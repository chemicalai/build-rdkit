SET THISDIR=%~dp0

REM Customize the following lines.

SET MODEL=64
SET RDKITDIR=%THISDIR%rdkit-Release_2019_03_3
SET BOOSTDIR=%THISDIR%boost_1_70_0
SET EIGENDIR=%THISDIR%eigen-eigen-323c052e1731
SET ZLIBDIR=%THISDIR%zlib-1.2.11
IF "%MODEL%" EQU "64" SET PYTHONDIR=C:\Python36
IF "%MODEL%" EQU "32" (
	ECHO 32-bit build is not supported.
	GOTO :END
)

REM End Customize

IF "%MODEL%" EQU "64" GOTO :MODEL64
IF "%MODEL%" EQU "32" GOTO :MODEL32
ECHO Unknown model "%MODEL%".
GOTO :END 

:MODEL64
SET CMAKEG=Visual Studio 15 2017 Win64
SET MSBUILDPLATFORM=x64
GOTO :ENDMODEL

:MODEL32
SET CMAKEG=Visual Studio 15 2017
SET MSBUILDPLATFORM=x86
GOTO :ENDMODEL

:ENDMODEL
SET BUILDDIR=build%MODEL%

IF "%$PythonPathAdded%" NEQ "" GOTO :ENDADDPYTHONPATH
PATH %PYTHONDIR%;%Path%
SET $PythonPathAdded=1
:ENDADDPYTHONPATH

PUSHD %PYTHONDIR%
.\Scripts\pip.exe install numpy
POPD

CALL make-zlib.bat
CALL make-boost.bat
CALL make-rdkit

IF "%$PythonPathAdded2%" NEQ "" GOTO :ENDADDPYTHONPATH2
SET PYTHONPATH=%RDKITDIR%;%PYTHONPATH%
SET $PythonPathAdded2=1
:ENDADDPYTHONPATH2

IF "%$BoostPathAdded%" NEQ "" GOTO :ENDBOOSTPATHADDED
PATH %BOOSTDIR%\stage\%MSBUILDPLATFORM%\lib;%Path%
SET $BoostPathAdded=1
:ENDBOOSTPATHADDED

:END
